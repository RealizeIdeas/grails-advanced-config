buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.6'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

version '1.0'
group 'net.realizeideas'

apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.yaml:snakeyaml:1.19'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

asciidoctor {
    attributes 'version': project.version,
            'groupId': project.group,
            'artifactId': project.name
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'grails-plugins'
        name = project.name
        desc = 'Grails Advanced Config Plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/RealizeIdeas/grails-advanced-config.git'
        issueTrackerUrl = 'https://github.com/RealizeIdeas/grails-advanced-config/issues'
        version {
            attributes = ['grails-plugin': "$project.group:$project.name"]
            name = project.version
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId project.name
            groupId 'net.realizeideas'
            from components.java
        }
    }
}